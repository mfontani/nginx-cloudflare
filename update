#!/usr/bin/env perl
use strict;
use warnings;
use Mojo::UserAgent;

my $ua  = Mojo::UserAgent->new;
my @ips;
for my $version (qw<4 6>) {
    my $tx  = $ua->get("https://www.cloudflare.com/ips-v$version");
    die $tx->error
        if $tx->error;
    push @ips, split("\n", $tx->res->body);
}

open my $fh, '>', 'cloudflare.conf'
    or die "Could not open cloudflare.conf for writing: $!";

print $fh <<'END_HEADER';
# These are the IPs Cloudflare uses as source for their service.
# If a request comes from these IP addresses, then the header:
# CF-Connecting-IP
# contains the IP address of the original requester.
# The original list is located at https://www.cloudflare.com/ips
# and should be routinely checked.
END_HEADER
print $fh "set_real_ip_from   $_;\n"
    for sort @ips;
print $fh "real_ip_header     CF-Connecting-IP;\n";
close $fh
    or die "Could not close filehandle: $!";
