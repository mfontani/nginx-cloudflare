#!/usr/bin/env perl
use strict;
use warnings;
use Mojo::UserAgent;
use Carp qw<croak>;

my $HEADER = <<'END_HEADER';
# These are the IPs Cloudflare uses as source for their service.
# If a request comes from these IP addresses, then the header:
# CF-Connecting-IP
# contains the IP address of the original requester.
# The original list is located at https://www.cloudflare.com/ips
# and should be routinely checked.
END_HEADER

my $ua  = Mojo::UserAgent->new;
my @ips;
for my $version (qw<4 6>) {
    my $tx  = $ua->get("https://www.cloudflare.com/ips-v$version");
    croak $tx->error
        if $tx->error;
    push @ips, split /\n/xms, $tx->res->body;
}

open my $fh, '>', 'cloudflare.conf'
    or croak "Could not open cloudflare.conf for writing: $!";

print { $fh } $HEADER;
print { $fh } "set_real_ip_from   $_;\n"
    for sort @ips;
print { $fh } "real_ip_header     CF-Connecting-IP;\n";
close $fh
    or croak "Could not close filehandle: $!";
